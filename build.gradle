import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id 'idea'
  id 'org.zeroturnaround.gradle.jrebel' version '1.1.5'
  id 'ninja.miserable.blossom' version '1.0.1'
  id 'com.github.johnrengelman.shadow' version '2.0.1'
}

String packagePath = 'com.herocc.bukkit.multispawn'
group packagePath
version '1.3.0'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.7


repositories {
  mavenCentral()
  maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
  maven { url 'https://repo.bstats.org/content/repositories/releases' }
}

dependencies {
  compile group: 'org.bukkit', name: 'bukkit', version: '1.12-R0.1-SNAPSHOT'
  compile group: 'org.bstats', name: 'bstats-bukkit-lite', version: '1.1'
}

artifacts { // task 'build' runs generates uberjar
  archives shadowJar
}

jar {
  compileJava.options.encoding = 'UTF-8' // Use UTF8 Encodings, because standards
  version = version + '-min' // we want the Uberjar to be distributed, this is the minified version
  dependsOn generateRebel // Automatically generate jRebel configs
}

shadowJar {
  dependencies {
    include dependency('org.bstats:bstats-bukkit-lite')
  }
  relocate('org.bstats', packagePath + '.dependencies')
  classifier = null
  dependsOn generateRebel
}

idea {
  module {
    downloadJavadoc = true // Automatically download javadocs
  }
}

ext {
  // Placeholders for configuration filtering
  description = 'Teleports players to a random spawn on join'
  author = 'HeroCC <https://herocc.com>'

  javaPackage = packagePath
  resourceTokens = [
      'Name'   : rootProject.name,
      'Description': description,
      'Version': version,
      'Author' : author
  ];
}

//noinspection GroovyAssignabilityCheck
processResources {
  include 'plugin.yml', 'config.yml'
  filter ReplaceTokens, tokens: resourceTokens
}

blossom {
  replaceToken '@Version@', project.version, "src/main/java/${javaPackage}/utils/Constants.java" //TODO Set this up
}
