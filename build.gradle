import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
  id 'java-library'
  id 'ninja.miserable.blossom' version '1.0.1'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

String packagePath = 'com.herocc.bukkit.multispawn'
group packagePath
version '1.4.0'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

repositories {
  jcenter()
  maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
  maven { url 'https://papermc.io/repo/repository/maven-public/' }
  maven { url 'https://repo.codemc.org/repository/maven-public/' }
  maven { url 'https://jitpack.io' }
}

dependencies {
  compileOnly 'org.bukkit:bukkit:1.15.2-R0.1-SNAPSHOT'
  implementation "io.papermc:paperlib:1.0.2"
  implementation 'org.bstats:bstats-bukkit-lite:1.7'

  testImplementation 'junit:junit:4.13'
  testImplementation 'com.github.seeseemelk:MockBukkit:8bc786b8b42bf99a6ff0a37c3d35ae577e60948d'
}

artifacts { // task 'build' runs generates uberjar
  archives shadowJar
}

jar {
  compileJava.options.encoding = 'UTF-8' // Use UTF8 Encodings, because standards
  archiveClassifier = 'min' // we want the Uberjar to be distributed, this is the minified version
}

task relocateShadowJar(type: ConfigureShadowRelocation) {
  target = tasks.shadowJar
  prefix = packagePath + ".dependencies"
}

shadowJar {
  dependsOn tasks.relocateShadowJar
  archiveClassifier = null
}

ext {
  // Placeholders for configuration filtering
  description = 'Teleports players to a random spawn on join'
  author = 'HeroCC <https://herocc.com>'

  javaPackage = packagePath
  resourceTokens = [
      'Name'   : rootProject.name,
      'Description': description,
      'Version': version,
      'Author' : author
  ];
}

//noinspection GroovyAssignabilityCheck
processResources {
  include 'plugin.yml', 'config.yml'
  filter ReplaceTokens, tokens: resourceTokens
}

blossom {
  replaceToken '@Version@', project.version, "src/main/java/${javaPackage}/utils/Constants.java" //TODO Set this up
}
