import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id 'idea'
  id 'org.zeroturnaround.gradle.jrebel' version '1.1.3'
  id 'ninja.miserable.blossom' version '1.0.1'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'MultiSpawn'
version '1.2.1'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.7


repositories {
  mavenCentral()
  maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
  maven { url 'http://repo.mcstats.org/content/repositories/public' }
}

dependencies {
  compile group: 'org.bukkit', name: 'bukkit', version: '1.9.4-R0.1-SNAPSHOT'
  compile group: 'org.mcstats.bukkit', name: 'metrics', version: 'R7'
}

artifacts { // task 'build' runs generates uberjar
  archives shadowJar
}

//noinspection GroovyAssignabilityCheck
jar {
  version = version + '-min' // we want the Uberjar to be distributed, this is the minified version
  dependsOn generateRebel // Automatically generate jRebel configs
}

shadowJar {
  dependencies {
    include dependency('org.mcstats.bukkit:metrics')
  }
  classifier = null
  dependsOn generateRebel
}

idea {
  module {
    downloadJavadoc = true // Automatically download javadocs
  }
}

ext {
  // Placeholders for configuration filtering
  description = 'Teleports players to a random spawn on join'
  author = 'HeroCC <https://herocc.com>'

  javaPackage = 'com.herocc.bukkit.multispawn'
  resourceTokens = [
      'Name'   : rootProject.name,
      'Description': description,
      'Version': version,
      'Author' : author
  ];
}

//noinspection GroovyAssignabilityCheck
processResources {
  include 'plugin.yml', 'config.yml'
  filter ReplaceTokens, tokens: resourceTokens
}

blossom {
  replaceToken '@Version@', project.version, "src/main/java/${javaPackage}/utils/Constants.java" //TODO Set this up
}
