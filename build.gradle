import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java'
  id 'idea'
  id 'ninja.miserable.blossom' version '1.0.1'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

String packagePath = 'com.herocc.bukkit.multispawn'
group packagePath
version '1.3.1'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8


repositories {
  jcenter()
  maven { url 'https://hub.spigotmc.org/nexus/content/groups/public' }
  maven { url 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
  compile 'org.bukkit:bukkit:1.14-R0.1-SNAPSHOT'
  compile 'org.bstats:bstats-bukkit-lite:1.7'
}

artifacts { // task 'build' runs generates uberjar
  archives shadowJar
}

jar {
  compileJava.options.encoding = 'UTF-8' // Use UTF8 Encodings, because standards
  version = version + '-min' // we want the Uberjar to be distributed, this is the minified version
}

shadowJar {
  dependencies {
    include dependency('org.bstats:bstats-bukkit-lite')
  }
  relocate('org.bstats', packagePath + '.dependencies')
  classifier = null
}

idea {
  module {
    downloadJavadoc = true // Automatically download javadocs
  }
}

ext {
  // Placeholders for configuration filtering
  description = 'Teleports players to a random spawn on join'
  author = 'HeroCC <https://herocc.com>'

  javaPackage = packagePath
  resourceTokens = [
      'Name'   : rootProject.name,
      'Description': description,
      'Version': version,
      'Author' : author
  ];
}

//noinspection GroovyAssignabilityCheck
processResources {
  include 'plugin.yml', 'config.yml'
  filter ReplaceTokens, tokens: resourceTokens
}

blossom {
  replaceToken '@Version@', project.version, "src/main/java/${javaPackage}/utils/Constants.java" //TODO Set this up
}
